Main = fromCountingRelToFunctionsAndSets;expand!;delete!;addSet;addFunction


fromCountingRelToFunctionsAndSets(specName,decisionVariableName:string;findPos:int)
[
     // DecisionVariableType becomes function

    (n0, specName)
    (n1, "find")
    (n2, decisionVariableName)
    (n3, "relation")
|
    (e1, n0, n1, findPos)
    (e2, n1, n2, 1)
    (e3, n2, n3, 1)
]
=>
[
    (n0, specName)
    (n1, "find")
    (n2, decisionVariableName)
    (n3, "relation"# red)
|
    (e1, n0, n1, findPos)
    (e2, n1, n2, 1)
    (e3, n2, n3, 1)
]
interface =
{
  n0,n1, n2, n3
}

expand(x,y:string; n:int) 
[ 
(n0, x # red) 
(n1, y) 
| 
(e1, n0, n1, n) 
] 
=> 
[ 
(n0, x # red) 
(n1, y # red) 
| 
(e1, n0, n1, n) 
]
interface = {n0, n1}

delete(x,y:string; n:int)
[ 
(n0, x # red) 
(n1, y # red) 
| 
(e1, n0, n1, n) 
] 
=> 
[ 
(n0, x # red) 
| 
]
interface = {n0}

addSet(specName:string)
[ 
(n0, specName) 
(n1, "root") 
| 
(e1, n0, n1, 0) 
] 
=> 
[
(n0,specName)
(n1,"root")
(n111,"letting")
(n10,"DomainNameLettingStatement")
(n2,"coloursSet")
(n11,"Node")
(n3,"set")
(n12,"SetDomain")
(n4,"size")
(n13,"Attribute")
(n5,"coloursPerNode")
(n14,"ReferenceToParameter")
(n6,"colours")
(n15,"ReferenceToNamedDomain")

| 
(e0,n0,n1,0)
(e1,n0,n111,1)
(e111,n111,n2,1)
(e2,n2,n3,1)
(e3,n3,n4,1)
(e4,n4,n5,1)
(e5,n3,n6,2)


(e17,n1,n10, 0)
(e18,n2,n11, 0)
(e19,n3,n12, 0)
(e20,n4,n13, 0)
(e21,n5,n14, 0)
(e22,n6,n15, 0)

]
interface = {n0,n1}

addFunction()
[
     // DecisionVariableType becomes function

    (n0, "relation"# red)
|

]
=>
[
    (n0, "function")
    (n1, "FunctionDomain")
    (n2, "total")
    (n3, "vertices")
    (n4, "coloursSet")

    (n12, "Attribute")
    (n13, "ReferenceToNamedDomain")
    (n14, "ReferenceToNamedDomain")

|
    (e0, n0, n1, 0)
    (e2, n0, n2, 1)
    (e3, n0, n3, 2)
    (e4, n0, n4, 3)
    
    (e12, n2, n12, 0)
    (e13, n3, n13, 0)
    (e14, n4, n14, 0)
]
interface =
{
  n0
}

